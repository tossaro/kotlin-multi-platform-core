plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'org.jetbrains.dokka' version "1.7.20"
    id "kotlinx-serialization"
}

apply from: '../properties.gradle'

android {
    namespace "${setup.appId}.example"
    compileSdkVersion setup.android.compileSdkVersion

    defaultConfig {
        applicationId "${setup.appId}.example"
        minSdkVersion setup.android.minSdkVersion
        targetSdkVersion setup.android.targetSdkVersion
        versionCode buildVersionCode()
        versionName buildVersionName()

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding = true
    }

    flavorDimensions 'api'
    productFlavors {
        dev {
            dimension 'api'
            applicationIdSuffix setup.suffix.appId.dev
            versionNameSuffix setup.suffix.versionName.dev
            resValue "string", "app_version", String.format("%s%s", "${defaultConfig.versionName}", "${versionNameSuffix}")
            resValue "string", "app_name", String.format("%s", setup.appName.dev)
            buildConfigField 'String', 'SERVER', String.format("%s", server.dev)
        }
        stage {
            dimension 'api'
            applicationIdSuffix setup.suffix.appId.stage
            versionNameSuffix setup.suffix.versionName.stage
            resValue "string", "app_version", String.format("%s%s", "${defaultConfig.versionName}", "${versionNameSuffix}")
            resValue "string", "app_name", String.format("%s", setup.appName.stage)
            buildConfigField 'String', 'SERVER', String.format("%s", server.stage)
        }
        beta {
            dimension 'api'
            applicationIdSuffix setup.suffix.appId.beta
            versionNameSuffix setup.suffix.versionName.beta
            resValue "string", "app_version", String.format("%s%s", "${defaultConfig.versionName}", "${versionNameSuffix}")
            resValue "string", "app_name", String.format("%s", setup.appName.beta)
            buildConfigField 'String', 'SERVER', String.format("%s", server.beta)
        }
        store {
            dimension 'api'
            resValue "string", "app_version", "${defaultConfig.versionName}"
            resValue "string", "app_name", String.format("%s", setup.appName.store)
            buildConfigField 'String', 'SERVER', String.format("%s", server.store)
        }
    }

    task installLocalGitHook(type: Copy) {
        from new File(rootProject.rootDir, 'scripts/pre-commit')
        into { new File(rootProject.rootDir, '.git/hooks') }
        fileMode 0775
    }
    build.dependsOn installLocalGitHook
}

dokkaGfm {
    suppressInheritedMembers.set(true)
    outputDirectory.set(file("${rootProject.rootDir}/docs/${project.name}"))
}

dependencies {
    implementation project(':core_shared')
    // uncomment below implementation if using local package
    implementation project(":example_lib")

    dep.each {
        if (it.configuration == "platform") {
            add("implementation", platform(it.dependency))
        } else {
            add(it.configuration, it.dependency, it.options)
        }
    }
}