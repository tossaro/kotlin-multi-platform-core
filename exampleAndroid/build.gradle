plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'org.jetbrains.dokka' version "1.7.10"
    id "kotlinx-serialization"
}

apply from: '../properties.gradle'

android {
    namespace "${setup.appId}.example"
    compileSdkVersion setup.compileSdkVersion

    defaultConfig {
        applicationId "${setup.appId}.example"
        minSdkVersion setup.minSdkVersion
        targetSdkVersion setup.targetSdkVersion
        versionCode buildVersionCode()
        versionName buildVersionName()

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        multiDexEnabled true

        //routes
        resValue "string", "route_stocks", String.format("%s%s", deeplink, "/stocks")
        resValue "string", "route_stock_detail_sheet", String.format("%s%s", deeplink, "/stock/{coin}/{value}")
        resValue "string", "route_stock_edit", String.format("%s%s", deeplink, "/stock/{coin}")
        resValue "string", "route_order", String.format("%s%s", deeplink, "/order")
        resValue "string", "route_date_sheet", String.format("%s%s", deeplink, "/select-date/{title}/{selected}/{min}/{max}")
        resValue "string", "route_profile", String.format("%s%s", deeplink, "/profile")
        resValue "string", "route_signout_dialog", String.format("%s%s", deeplink, "/sign-out")
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding = true
    }

    flavorDimensions 'api'
    productFlavors {
        dev {
            dimension 'api'
            applicationIdSuffix setup.suffix.appId.dev
            versionNameSuffix setup.suffix.versionName.dev
            resValue "string", "app_version", String.format("%s%s", "${defaultConfig.versionName}", "${versionNameSuffix}")
            resValue "string", "app_name", String.format("%s", setup.appName.dev)
            buildConfigField 'String', 'SERVER', String.format("%s", server.dev)
            buildConfigField 'String', 'SOCKET', String.format("%s", socket.dev)
        }
        stage {
            dimension 'api'
            applicationIdSuffix setup.suffix.appId.stage
            versionNameSuffix setup.suffix.versionName.stage
            resValue "string", "app_version", String.format("%s%s", "${defaultConfig.versionName}", "${versionNameSuffix}")
            resValue "string", "app_name", String.format("%s", setup.appName.stage)
            buildConfigField 'String', 'SERVER', String.format("%s", server.stage)
            buildConfigField 'String', 'SOCKET', String.format("%s", socket.stage)
        }
        beta {
            dimension 'api'
            applicationIdSuffix setup.suffix.appId.beta
            versionNameSuffix setup.suffix.versionName.beta
            resValue "string", "app_version", String.format("%s%s", "${defaultConfig.versionName}", "${versionNameSuffix}")
            resValue "string", "app_name", String.format("%s", setup.appName.beta)
            buildConfigField 'String', 'SERVER', String.format("%s", server.beta)
            buildConfigField 'String', 'SOCKET', String.format("%s", socket.beta)
        }
        store {
            dimension 'api'
            resValue "string", "app_version", "${defaultConfig.versionName}"
            resValue "string", "app_name", String.format("%s", setup.appName.store)
            buildConfigField 'String', 'SERVER', String.format("%s", server.store)
            buildConfigField 'String', 'SOCKET', String.format("%s", socket.store)
        }
    }
}

dokkaGfm {
    suppressInheritedMembers.set(true)
    outputDirectory.set(file("${rootProject.rootDir}/docs/${project.name}"))
}

dependencies {
    // uncomment below implementation if using local package
    implementation project(":shared")

    dep.each {
        if (it.configuration == "platform") {
            add("implementation", platform(it.dependency))
        } else {
            add(it.configuration, it.dependency, it.options)
        }
    }
}